PDefs.   	Program ::= [Def] ;
terminator  Def ""			  ;

DFun.		Def		::= Type Id "(" [Arg] ")" "{" [Stm] "}" ;
DFunCall.	Def		::= Type Id "(" [Arg] ")" ";" ;

separator   Arg     "," 	  ;
separator nonempty Decl ","   ;

terminator  Stm ""			  ;

DNameSpace. Def     ::= "using" QConsts ";";
DDecls. 	Def		::= Type [Decl] ";" ; 
STypeDefId. Stm     ::= "typedef" Type Id ";"; 
DTypeDefId. Def     ::= "typedef" Type Id ";";

ADecl. 	    Arg     ::= Type ;
ADeclId. 	Arg     ::= Type Id ;
SDecls. 	Stm		::= Type [Decl] ";" ;
DId.        Decl    ::= Id;
DeclInit.   Decl    ::= Id "=" Exp;
SRefInit. 	Decl	::= "&" Id "=" Id ;
DInit. 		Def		::= Type Id "=" Exp ";" ;
SInit. 		Stm		::= Type Id "=" Exp ";" ;
SEexp.		Stm		::= Exp ";" ;
SReturn. 	Stm		::= "return" Exp ";" ;
SWhile. 	Stm		::= "while" "(" Exp ")" Stm;
SEWhile. 	Stm		::= "while" "(" Exp ")" ";";
SBlock. 	Stm 	::= "{" [Stm] "}" ;
SIfElse. 	Stm 	::= "if" "(" Exp ")" Stm Else ; 
SEElse.     Else    ::= "";
SElse.      Else    ::= "else" Stm; -- 1 shift 1 reduce
SConstRefInit.  Stm	::= "const" Type "&" Id "=" Exp ";" ;
SConstRef.  Stm	::= "const" Type "&" Id ";" ;
SFor.           Stm ::= "for" "(" Type Id "=" Exp ";" Exp ";" Exp ")" Stm ;
SFor2.          Stm ::= "for" "(" Type Id ";" Exp ";" Exp ")" Stm ;
SFor3.           Stm ::= "for" "(" "const" Type Id "=" Exp ";" Exp ";" Exp ")" Stm ;
SConstInitId.   Stm ::= "const" Type Id "=" Exp ";" ;
SConstId.   Stm ::= "const" Type Id";" ;
SDo.		    Stm	::=	"do" Stm "while" "("Exp")" ";" ; 


AConstRefTypeId. Arg ::= "const" Type "&" Id;
AConstTypeId.    Arg ::= "const" Type Id;
ARefId.          Arg ::= Type "&" Id ;
AConstRefType.   Arg ::= "const" Type "&";
ARefType.        Arg ::=  Type "&";




EChar. 		Exp15 	::= Char ;
EInt. 		Exp15 	::= Integer ;
EDouble. 	Exp15 	::= Double  ;
EString. 	Exp15   ::= StringList;
ETrue. 		Exp15 	::= "true"  ;
EFalse.		Exp15 	::= "false" ;
EConst.		Exp15   ::= QConsts ;

EIndex.		Exp14   ::= Exp14 	"[" Exp "]" ; 
ECall.		Exp14   ::= Exp14 	"(" [Exp] ")" ; 
EFun.		Exp14   ::= Exp14 "." Exp15; 
EPIncr.		Exp14	::= Exp14 "++" ;
EPDcecr.	Exp14	::= Exp14 "--" ;
EArrow.	    Exp14	::= Exp14 "->" Exp15;


EDeref. 	Exp13   ::= "*"  Exp13;
ENot. 		Exp13   ::= "!"  Exp13 ;
EIncr. 		Exp13   ::= "++" Exp13 ;
EDecr.		Exp13 	::= "--" Exp13 ;

ENeg.		Exp12   ::= "-"  Exp13 ;
EMul.		Exp12   ::= Exp12 "*" Exp13 ;
EDiv. 		Exp12 	::= Exp12 "/" Exp13 ;
EMod. 		Exp12 	::= Exp12 "%" Exp13 ;
EAdd.		Exp11   ::= Exp11 "+" Exp12 ;
ESub.		Exp11	::= Exp11 "-" Exp12 ;
ELt.		Exp9 	::= Exp9  "<" Exp10 ;
EGt.		Exp9 	::= Exp9  ">" Exp10 ;
ELEq.		Exp9 	::= Exp9  "<=" Exp10;
EGEq.		Exp9 	::= Exp9  ">=" Exp10;
EEq.		Exp8 	::= Exp8  "==" Exp9 ;
ENEq.		Exp8	::= Exp8  "!=" Exp9 ;
EAnd.		Exp4 	::= Exp4  "&&" Exp5 ;
EOr.		Exp3    ::= Exp3  "||" Exp4 ;
EAss.		Exp2    ::= Exp3  "="  Exp2 ;
EAss2.		Exp2    ::= Exp3  "-="  Exp2 ;
EAss3.		Exp2    ::= Exp3  "+="  Exp2 ;
EQstnmrk.	Exp2    ::= Exp3 "?" Exp2 ":" Exp2 ;

EThrowE.	Exp1 	::= "throw"    Exp1 ;

ELl.		Exp10	::= Exp10 "<<" Exp11 ; 
EGg.		Exp10	::= Exp10 ">>" Exp11 ;



coercions Exp 15  ;
separator Exp "," ;

Tbool. 	 Type ::= "bool"   ;
Tdouble. Type ::= "double" ;
Tint.	 Type ::= "int"    ;
Tvoid.	 Type ::= "void"   ;
TQConst. Type ::= QConsts  ;


QCnst.  QConst  ::= Id;
QCnsts. QConsts ::= [QConst];
separator nonempty  QConst "::";

StrList. StringList ::= [String];
separator nonempty String "";

token Id (letter (letter | digit | '_')*) ;



comment     "//"			  ;
comment     "/*" "*/"		  ; 
comment     "#"				  ;